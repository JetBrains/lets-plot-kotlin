buildscript {
    dependencies {
        classpath("org.yaml:snakeyaml:1.25")
    }
}

plugins {
    id "org.jetbrains.kotlin.multiplatform" apply false
    id 'org.jetbrains.kotlin.jvm' apply false
    id "io.codearte.nexus-staging"
}

/*
 * Copyright (c) 2021. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

import org.yaml.snakeyaml.Yaml

def build_settings_file = new File(rootDir, "build_settings.yml")
if (!build_settings_file.canRead()) {
    throw new GradleException("Couldn't read build_settings.yml")
}
def settings = new Yaml().load(build_settings_file.newInputStream())
project.ext.buildSettings = settings


allprojects {
    group 'org.jetbrains.lets-plot'
    version "3.0.3-alpha1"

    ext.versionIsDev = (version.contains('SNAPSHOT')
            || version.contains('rc')
            || version.contains('alpha')
            || version.contains('beta'))

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    repositories {
        maven {
            // GeoTools repository must be before Maven Central
            // See: https://stackoverflow.com/questions/26993105/i-get-an-error-downloading-javax-media-jai-core1-1-3-from-maven-central
            // See also Jupyter Kotlin issue: https://github.com/Kotlin/kotlin-jupyter/issues/107
            url "https://repo.osgeo.org/repository/release"
        }

        mavenCentral()

        // local
//        maven {
//            url = uri("/Users/Igor/Work/lets-plot/.maven-publish-dev-repo")
//        }

        // SNAPSHOTS
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    // Maven publication settings

    // define local Maven Repository path:
    project.ext.localMavenRepository = "$rootDir/.maven-publish-dev-repo"

    // define Sonatype nexus repository manager settings:
    def sonatypeSnapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
    def sonatypeReleaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    project.ext.sonatypeUrl = version.contains('SNAPSHOT') ? sonatypeSnapshotUrl : sonatypeReleaseUrl

    // nexus-staging plugin settings:
    nexusStaging {
        packageGroup = 'org.jetbrains'
        username = project.buildSettings?.sonatype?.username
        password = project.buildSettings?.sonatype?.password
    }

}